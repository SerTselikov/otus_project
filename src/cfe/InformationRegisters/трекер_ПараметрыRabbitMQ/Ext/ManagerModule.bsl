
Функция ИменаПараметров() Экспорт
	ИменаПараметров = Новый Массив;
	ИменаПараметров.Добавить("Адрес");
	ИменаПараметров.Добавить("Порт");
	ИменаПараметров.Добавить("Логин");
	ИменаПараметров.Добавить("Пароль");
	ИменаПараметров.Добавить("ВиртуальныйХост");
	ИменаПараметров.Добавить("ТочкаОбмена");
	ИменаПараметров.Добавить("ИмяОчереди");
	ИменаПараметров.Добавить("КлючМаршрутизации");
	Возврат ИменаПараметров;
КонецФункции

Функция ЗначенияПараметров() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыRabbitMQ.Имя КАК Имя,
	|	ПараметрыRabbitMQ.Аналитика КАК Аналитика,
	|	ПараметрыRabbitMQ.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.трекер_ПараметрыRabbitMQ КАК ПараметрыRabbitMQ";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ЗначенияПараметров = Новый Структура;
	Пока Выборка.Следующий() Цикл
		Значение = Неопределено;
		ЗначенияПараметров.Свойство(Выборка.Имя, Значение);
		Если Значение = Неопределено Тогда
			Если ЗначениеЗаполнено(Выборка.Аналитика) Тогда
				КоллекцияЗначений = Новый Соответствие;
				КоллекцияЗначений.Вставить(Выборка.Аналитика, Выборка.Значение);
				ЗначенияПараметров.Вставить(Выборка.Имя, КоллекцияЗначений);
			Иначе
				ЗначенияПараметров.Вставить(Выборка.Имя, Выборка.Значение);
			КонецЕсли;
		Иначе
			Если ЗначениеЗаполнено(Выборка.Аналитика) Тогда
				Если ТипЗнч(Значение) = Тип("Соответствие") Тогда
					КоллекцияЗначений = Значение;
				Иначе
					КоллекцияЗначений = Новый Соответствие;
					КоллекцияЗначений.Вставить("", Значение);
				КонецЕсли;
				КоллекцияЗначений.Вставить(Выборка.Аналитика, Выборка.Значение);
			Иначе
				Если ТипЗнч(Значение) = Тип("Массив") Тогда
					КоллекцияЗначений = Значение;
				Иначе
					КоллекцияЗначений = Новый Массив;
					КоллекцияЗначений.Добавить(Значение);
				КонецЕсли;
				КоллекцияЗначений.Добавить(Выборка.Значение);
			КонецЕсли;
			
			ЗначенияПараметров.Вставить(Выборка.Имя, КоллекцияЗначений);
		КонецЕсли;
	КонецЦикла;
	Возврат ЗначенияПараметров;
КонецФункции

Процедура ЗаполнитьДоступныеПараметры() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗИмена.Имя КАК Имя
	|ПОМЕСТИТЬ ВТ_Имена
	|ИЗ
	|	&ТЗИмена КАК ТЗИмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Имена.Имя КАК Имя
	|ИЗ
	|	ВТ_Имена КАК ВТ_Имена
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.трекер_ПараметрыRabbitMQ КАК ПараметрыRabbitMQ
	|		ПО ВТ_Имена.Имя = ПараметрыRabbitMQ.Имя
	|ГДЕ
	|	ПараметрыRabbitMQ.Значение ЕСТЬ NULL";
	
	ТЗИмена = Новый ТаблицаЗначений;
	ТЗИмена.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	Для Каждого ИмяПараметра Из РегистрыСведений.трекер_ПараметрыRabbitMQ.ИменаПараметров() Цикл
		ТЗИмена.Добавить().Имя = ИмяПараметра;
	КонецЦикла;
	Запрос.УстановитьПараметр("ТЗИмена", ТЗИмена);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.трекер_ПараметрыRabbitMQ.СоздатьМенеджерЗаписи();	
		МенеджерЗаписи.Имя = Выборка.Имя;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры
