
#Область СлужебныйПрограммныйИнтерфейс
 
Процедура ЗагрузкаЗаявокНаИзменениеОбработчикЗадания() Экспорт
	ЗагрузитьЗаявкиИзRabbitMQ();
КонецПроцедуры

Процедура ЗагрузитьЗаявкиИзRabbitMQ() Экспорт
	ПараметрыСообщения = RabbitMQКлиентСервер.НовыеПараметрыСообщения();
	ПараметрыПодключения = РегистрыСведений.ПараметрыRabbitMQ.ЗначенияПараметров();
	ЗаполнитьЗначенияСвойств(ПараметрыСообщения, ПараметрыПодключения);
	ПараметрыСообщения.ИмяОчереди = ПараметрыПодключения.ИмяОчередиЗаявки;
	ПараметрыСообщения.КлючМаршрутизации = ПараметрыПодключения.КлючМаршрутизацииЗаявки;
	
	КлиентКомпоненты = RabbitMQ.ПолучитьКомпоненту();
	Сообщение = 0;
	КоличествоЗаявок = 0;
	Пока Сообщение <> Неопределено Цикл
		Если Сообщение <> 0 Тогда
			ДанныеСообщения = КоннекторHTTP.JsonВОбъект(Сообщение,);
			ОписаниеЗаявки = БизнесПроцессы.ЗаявкаНаИзменение.НовоеОписаниеЗаявки(
				ДанныеСообщения.Получить("Заказчик"),
				ДанныеСообщения.Получить("ПрограммныйПродукт"),
				ДанныеСообщения.Получить("Описание"));
			БизнесПроцессы.ЗаявкаНаИзменение.СоздатьИСтартовать(ОписаниеЗаявки);
			КоличествоЗаявок = КоличествоЗаявок + 1;
		КонецЕсли;
		Сообщение = RabbitMQКлиентСервер.ПрочитатьСообщение(КлиентКомпоненты, ПараметрыСообщения);
	КонецЦикла;
	Сообщить(СтрШаблон("Загружено заявок на изменение: %1", КоличествоЗаявок));
КонецПроцедуры

#КонецОбласти
