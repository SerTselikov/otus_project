
#Область СлужебныйПрограммныйИнтерфейс

Процедура ПриЗаписиОбъектаОбменДаннымиОбработчикПодписки(Источник, Отказ) Экспорт
	
	ТипОбъекта = "";
	Если ТипЗнч(Источник) = Тип("СправочникОбъект.ПрограммныеПродукты") Тогда
		ТипОбъекта = "ПрограммныйПродукт";
	КонецЕсли;
	Если ЗначениеЗаполнено(ТипОбъекта) Тогда
		ТекстСообщения = Конвертер.ОбъектВТекстСообщения(Источник, ТипОбъекта);
		RabbitMQ.ОтправитьСообщение(ТекстСообщения, ТипОбъекта);
	КонецЕсли;
	
КонецПроцедуры
 
Процедура ЗагрузкаДанныхОбработчикЗадания() Экспорт
	ЗагрузитьДанные();
КонецПроцедуры

Процедура ЗагрузитьДанные() Экспорт
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	КоличествоЗаявок = 0;
	Кэш = Неопределено;
	
	Очереди = RabbitMQ.ОчередиДляПолученияСообщений();
	Для Каждого Очередь Из Очереди Цикл
		Пока Истина Цикл
			ТекстСообщения = RabbitMQ.ПолучитьСообщение(Очередь, Кэш);
			Если ТекстСообщения <> Неопределено Тогда
				Конвертер.ПреобразоватьТекстСообщенияВОбъект(ТекстСообщения);
				КоличествоЗаявок = КоличествоЗаявок + 1;
			Иначе
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ОбменДанными.ЗагрузитьДанные", НачалоЗамера, КоличествоЗаявок);
	
	Сообщить(СтрШаблон("Загружено заявок на изменение: %1", КоличествоЗаявок));
КонецПроцедуры

#КонецОбласти