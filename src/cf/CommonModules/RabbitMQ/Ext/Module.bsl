#Область ПрограммныйИнтерфейс

Процедура ЗагрузитьСообщения(ОбъектКонвертер) Экспорт
	НачалоЗамера = ОценкаПроизводительности.НачатьЗамерВремени();
	КоличествоЗаявок = 0;
	Кэш = Неопределено;
	
	Очереди = ОчередиДляПолученияСообщений();
	Для Каждого Очередь Из Очереди Цикл
		Пока Истина Цикл
			ТекстСообщения = ПолучитьСообщение(Очередь, Кэш);
			Если ТекстСообщения <> Неопределено Тогда
				ОбъектКонвертер.ПреобразоватьТекстСообщенияВОбъект(ТекстСообщения);
				КоличествоЗаявок = КоличествоЗаявок + 1;
			Иначе
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	ОценкаПроизводительности.ЗакончитьЗамерВремени("ОбменДанными.ЗагрузитьДанные", НачалоЗамера, КоличествоЗаявок);
	
	Сообщить(СтрШаблон("Загружено заявок на изменение: %1", КоличествоЗаявок));
КонецПроцедуры

Процедура СформироватьИОтправитьСообщение(Объект, ОбъектКонвертер) Экспорт
	ВспомогательныеПараметры = Новый Структура;
	ТекстСообщения = ОбъектКонвертер.ОбъектВТекстСообщения(Объект, ВспомогательныеПараметры);
	Если ТекстСообщения = Неопределено Тогда
		Возврат;	
	КонецЕсли;
	ОтправитьСообщение(ТекстСообщения, ВспомогательныеПараметры);
КонецПроцедуры

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

Процедура ОтправитьСообщение(ТекстСообщения, ДополнительныеПараметры) Экспорт
	КлючАналитики = ДополнительныеПараметры.ТипДанных;
	
	ПараметрыСообщения = RabbitMQКлиентСервер.НовыеПараметрыИсходящегоСообщения();
	ПараметрыПодключения = РегистрыСведений.ПараметрыRabbitMQ.ЗначенияПараметров();
	ЗаполнитьЗначенияСвойств(ПараметрыСообщения, ПараметрыПодключения);
	ПараметрыСообщения.КлючМаршрутизации = ПараметрыПодключения["КлючМаршрутизации"][КлючАналитики];
	
	ПараметрыСообщения.ТекстСообщения = ТекстСообщения;
	
	КлиентКомпоненты = ПолучитьКомпоненту();
	RabbitMQКлиентСервер.ОтправитьСообщение(КлиентКомпоненты, ПараметрыСообщения);
КонецПроцедуры

Функция ПолучитьАдресМакетаКомпановки(УникальныйИдентификатор) Экспорт
	
	МакетВнешнейКомпоненты    = Обработки.ПримерРаботыСКомпонентойPinkRabbitMQ.ПолучитьМакет("ВнешняяКомпонента");
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(МакетВнешнейКомпоненты, УникальныйИдентификатор);
	
	Возврат АдресВоВременномХранилище;
	
КонецФункции

Функция ПолучитьСообщение(ИмяОчереди, КэшируемыеЗначения = Неопределено) Экспорт
	Если КэшируемыеЗначения = Неопределено Тогда
		КэшируемыеЗначения = Новый Структура;
	КонецЕсли;
	
	ПараметрыСообщения = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КэшируемыеЗначения, "ПараметрыСообщения");
	Если ПараметрыСообщения = Неопределено Тогда
		ПараметрыСообщения = RabbitMQКлиентСервер.НовыеПараметрыВходящегоСообщения();
		ПараметрыПодключения = РегистрыСведений.ПараметрыRabbitMQ.ЗначенияПараметров();
		ЗаполнитьЗначенияСвойств(ПараметрыСообщения, ПараметрыПодключения);
	КонецЕсли;
	ПараметрыСообщения.ИмяОчереди = ИмяОчереди;
	КлиентКомпоненты = ОбщегоНазначенияКлиентСервер.СвойствоСтруктуры(КэшируемыеЗначения, "КлиентКомпоненты");
	Если КлиентКомпоненты = Неопределено Тогда
		КлиентКомпоненты = ПолучитьКомпоненту();
		КэшируемыеЗначения.Вставить("КлиентКомпоненты", КлиентКомпоненты);
	КонецЕсли;
	
	Сообщение = RabbitMQКлиентСервер.ПрочитатьСообщение(КлиентКомпоненты, ПараметрыСообщения);
	Возврат Сообщение;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ОчередиДляПолученияСообщений()
	ЗначенияПараметров = РегистрыСведений.ПараметрыRabbitMQ.ЗначенияПараметров();
	Если ТипЗнч(ЗначенияПараметров.ИмяОчереди) = Тип("Массив") Тогда
		Очереди = ЗначенияПараметров.ИмяОчереди; 
	Иначе
		Очереди = ОбщегоНазначенияКлиентСервер.ЗначениеВМассиве(ЗначенияПараметров.ИмяОчереди); 
	КонецЕсли;
	Возврат Очереди;
КонецФункции

Функция ПолучитьКомпоненту()
	
	КлиентКомпоненты = Неопределено;
	Если Не RabbitMQКлиентСервер.ИнициализироватьКомпоненту(КлиентКомпоненты) Тогда
		
		ПодключитьКомпоненту();
		RabbitMQКлиентСервер.ИнициализироватьКомпоненту(КлиентКомпоненты);
		
	КонецЕсли;
	
	Возврат КлиентКомпоненты;
КонецФункции

Процедура ПодключитьКомпоненту(КомпонентаПодключена = Неопределено, ВыводитьСообщения = Ложь)
	
	АдресВоВременномХранилище = ПолучитьАдресМакетаКомпановки(Новый УникальныйИдентификатор);
	КомпонентаПодключена = ПодключитьВнешнююКомпоненту(
			АдресВоВременномХранилище,
			"BITERP",
			ТипВнешнейКомпоненты.Native);
	Если ВыводитьСообщения Тогда
		Сообщить(НСтр("ru = 'Компонента подключена!'"));
	КонецЕсли;		
КонецПроцедуры

#КонецОбласти