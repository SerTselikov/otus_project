
#Область СлужебныйПрограммныйИнтерфейс

Функция ДоступныеСтатусы(ТекущийСтатус) Экспорт
	ДоступныеСтатусы = Новый Массив;
	Если НЕ ЗначениеЗаполнено(ТекущийСтатус) Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.Бэклог);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.Бэклог Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.АналитическаяПроработка);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.АналитическаяПроработка Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ДоработкаНеТребуется);
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ОценкаТрудозатрат);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.ОценкаТрудозатрат Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.СогласованиеДоработки);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.СогласованиеДоработки Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ДоработкаНеСогласована);
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ВРазработке);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.ВРазработке Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.Тестирование);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.Тестирование Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ВРазработке);
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.СозданиеЗапросаНаСлияние);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.СозданиеЗапросаНаСлияние Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ПроверкаКода);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.ПроверкаКода Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.ВРазработке);
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.СдачаРаботЗаказчику);
	ИначеЕсли ТекущийСтатус = Перечисления.СтатусыЗаявкиНаИзменение.СдачаРаботЗаказчику Тогда
		ДоступныеСтатусы.Добавить(Перечисления.СтатусыЗаявкиНаИзменение.Закрыта);
	КонецЕсли;
	
	Возврат ДоступныеСтатусы;
КонецФункции

// Создает новый экземпляр БП и стартует его
//
// Параметры:
//  ОписаниеЗаявки	 - Структура - см. НовоеОписаниеЗаявки()
//
Процедура СоздатьИСтартовать(ОписаниеЗаявки) Экспорт
	НовыйБП = БизнесПроцессы.ЗаявкаНаИзменение.СоздатьБизнесПроцесс();
	НовыйБП.Заполнить(ОписаниеЗаявки);
	НовыйБП.Записать();
	НовыйБП.Старт();
КонецПроцедуры

Функция НовоеОписаниеЗаявки(Заказчик, ПрограммныйПродукт, ПервичноеОбращение) Экспорт
	ОписаниеЗаявки = Новый Структура;
	ОписаниеЗаявки.Вставить("Заказчик", Заказчик);
	ОписаниеЗаявки.Вставить("ПрограммныйПродукт", ПрограммныйПродукт);
	ОписаниеЗаявки.Вставить("ПервичноеОбращение", ПервичноеОбращение);
	Возврат ОписаниеЗаявки;
КонецФункции

#КонецОбласти