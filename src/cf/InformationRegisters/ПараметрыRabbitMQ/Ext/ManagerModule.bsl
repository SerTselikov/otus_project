
Функция ИменаПараметров() Экспорт
	ИменаПараметров = Новый Массив;
	ИменаПараметров.Добавить("Адрес");
	ИменаПараметров.Добавить("Порт");
	ИменаПараметров.Добавить("Логин");
	ИменаПараметров.Добавить("Пароль");
	ИменаПараметров.Добавить("ВиртуальныйХост");
	ИменаПараметров.Добавить("ТочкаОбмена");
	ИменаПараметров.Добавить("ИмяОчередиПрограммныеПродукты");
	ИменаПараметров.Добавить("КлючМаршрутизацииПрограммныеПродукты");
	ИменаПараметров.Добавить("ИмяОчередиЗаявки");
	ИменаПараметров.Добавить("КлючМаршрутизацииЗаявки");
	Возврат ИменаПараметров;
КонецФункции

Функция ЗначенияПараметров() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ПараметрыRabbitMQ.Имя КАК Имя,
	|	ПараметрыRabbitMQ.Значение КАК Значение
	|ИЗ
	|	РегистрСведений.ПараметрыRabbitMQ КАК ПараметрыRabbitMQ";
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	ЗначенияПараметров = Новый Структура;
	Пока Выборка.Следующий() Цикл
		Значение = Неопределено;
		ЗначенияПараметров.Свойство(Выборка.Имя, Значение);
		Если Значение = Неопределено Тогда
			ЗначенияПараметров.Вставить(Выборка.Имя, Выборка.Значение);
		Иначе
			Если ТипЗнч(Значение) = Тип("Массив") Тогда
				МассивЗначений = Значение;
			Иначе
				МассивЗначений = Новый Массив;
				МассивЗначений.Добавить(Значение);
			КонецЕсли;
			МассивЗначений.Добавить(Выборка.Значение);
			
			ЗначенияПараметров.Вставить(Выборка.Имя, МассивЗначений);
		КонецЕсли;
	КонецЦикла;
	Возврат ЗначенияПараметров;
КонецФункции

Процедура ЗаполнитьДоступныеПараметры() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ТЗИмена.Имя КАК Имя
	|ПОМЕСТИТЬ ВТ_Имена
	|ИЗ
	|	&ТЗИмена КАК ТЗИмена
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_Имена.Имя КАК Имя
	|ИЗ
	|	ВТ_Имена КАК ВТ_Имена
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыRabbitMQ КАК ПараметрыRabbitMQ
	|		ПО ВТ_Имена.Имя = ПараметрыRabbitMQ.Имя
	|ГДЕ
	|	ПараметрыRabbitMQ.Значение ЕСТЬ NULL";
	
	ТЗИмена = Новый ТаблицаЗначений;
	ТЗИмена.Колонки.Добавить("Имя", Новый ОписаниеТипов("Строка",, Новый КвалификаторыСтроки(50)));
	Для Каждого ИмяПараметра Из РегистрыСведений.ПараметрыRabbitMQ.ИменаПараметров() Цикл
		ТЗИмена.Добавить().Имя = ИмяПараметра;
	КонецЦикла;
	Запрос.УстановитьПараметр("ТЗИмена", ТЗИмена);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		МенеджерЗаписи = РегистрыСведений.ПараметрыRabbitMQ.СоздатьМенеджерЗаписи();	
		МенеджерЗаписи.Имя = Выборка.Имя;
		МенеджерЗаписи.Записать();
	КонецЦикла;
	
КонецПроцедуры
